plugins {
    id 'java-library'
    id 'maven'

    id 'com.github.sherter.google-java-format' version '0.8'
}

version = '0.0.1'

repositories {
    jcenter()
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:28.0-jre'
    implementation 'com.google.firebase:firebase-admin:6.11.0'
    implementation 'com.github.jsqlparser:jsqlparser:3.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.2'
    testImplementation 'org.mockito:mockito-core:3.2.4'
}

test {
    useJUnitPlatform()
}

// Write pom.xml to publish this lib to maven repository
task writeNewPom {
    doLast {
        pom {
            project {
                groupId 'com.syucream'
                artifactId = 'firesql'

                inceptionYear '2020'
                licenses {
                    license {
                        name 'MIT'
                        url 'https://github.com/syucream/FireSQL/blob/master/LICENSE'
                        distribution 'repo'
                    }
                }
            }
        }.writeTo("$buildDir/libs/firesql-$version-pom.xml")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
